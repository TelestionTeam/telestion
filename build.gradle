plugins {
    id 'com.palantir.git-version' version '0.12.3'
}

allprojects {
    repositories {
        jcenter()
    }
}

subprojects {
    def details = versionDetails()
    version = details.lastTag + (details.commitDistance.toString() != '0' ? ('-' + details.commitDistance) : '')
    //version = gitVersion()
    group 'org.telestion'

    tasks.withType(JavaExec) {
        systemProperties System.properties
        systemProperties "hazelcast.logging.type":"slf4j"

        //enable modules for hazelcast
        jvmArgs += ["--add-modules", "java.se",
                    "--add-exports", "java.base/jdk.internal.ref=ALL-UNNAMED",
                    "--add-opens", "java.base/java.lang=ALL-UNNAMED",
                    "--add-opens", "java.base/java.nio=ALL-UNNAMED",
                    "--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED",
                    "--add-opens", "java.management/sun.management=ALL-UNNAMED",
                    "--add-opens", "jdk.management/com.sun.management.internal=ALL-UNNAMED"]
    }

    //enable preview features
    tasks.withType(JavaCompile) {
        options.compilerArgs += "--enable-preview"
    }
    tasks.withType(Test) {
        jvmArgs += "--enable-preview"
        ignoreFailures = true
        reports {
            junitXml.enabled = true
        }
    }
    tasks.withType(JavaExec) {
        jvmArgs += '--enable-preview'
    }
}


